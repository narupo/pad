{@
/*
    Created at 2020/10/09
*/

/**
 * Node of list
 */
struct Node:
    value = nil  // value of node
    prev = nil  // previous node
    next = nil  // next node
end

/**
 * Linked list structure
 */
struct List:
    head = nil
    tail = nil

    def new():
        return List()
    end

    met dump(self):
        for cur = self.head; cur; cur = cur.next:
            puts(cur.value)
        end
    end

    met push(self, value):
        if not self.head:
            self.head = Node()
            self.head.value = value
            self.tail = self.head
            return
        end

        tail = nil
        for cur = self.head; cur; cur = cur.next:
            tail = cur
        end

        if tail:
            node = Node()
            node.value = value
            node.prev = tail
            tail.next = node
            self.tail = node
        end
    end

    met pop(self):
        if not self.tail:
            return nil
        end

        tail = self.tail

        if tail.prev:
            tail.prev.next = nil
            self.tail = tail.prev
        else:
            self.head = nil
            self.tail = nil
        end

        tail.prev = nil
        return tail.value
    end
end
@}